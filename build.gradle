buildscript {

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "org.jetbrains.kotlin:kotlin-noarg:$kotlinVersion"
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
    id 'org.springframework.boot' version '2.0.1.RELEASE'
}

allprojects {

    apply plugin: 'kotlin'
    apply plugin: 'maven'

    group = 'net.alexanderkahn.longball'
    version = '0.0.1-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {

    compileKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = "1.8"
            javaParameters = true
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "PASSED", "FAILED", "SKIPPED"
        }
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

    apply from: "$rootProject.projectDir/dependencies.gradle"
}

dependencies {
    runtime project(':longball-core')
}

springBoot {
    mainClassName = 'net.alexanderkahn.longball.LongballServiceApplication'
}

bootRun {
    systemProperties = System.properties //pass through profiles from command line
}
